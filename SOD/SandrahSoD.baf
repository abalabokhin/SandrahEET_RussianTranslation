IF
AreaCheck("ar0602")
OR(2)
Global("SandrahJoined","GLOBAL",52)
Global("SandrahJoined","GLOBAL",6)
Global("SanstartAI","ar0602",0)
THEN
RESPONSE #100
SetGlobal("SanstartAI","ar0602",1)
ChangeAIScript("Sanact",DEFAULT)
END

IF
AreaCheck("bd0130")
OR(2)
PartyHasItem("bdshpam1")
PartyHasItem("bdshpam2")
OR(2)
Dead("BDKorme1")
Global("BD_MERC_MACHINE_OT","BD0130",3)
Global("SanSodPamp","BD0130",0)
THEN
RESPONSE #100
SetGlobal("SanSodPamp","BD0130",1)
END

IF
Global("bd_plot_003","bd0102",2)
Global("Sansusp","bd0102",0)
THEN
RESPONSE #100
SetGlobal("Sansusp","bd0102",1)
END

IF
Global("SanSodQuest","Global",0)
AreaCheck("bd1000")
THEN
RESPONSE #100
SetGlobal("SanSodQuest","Global",1)
END

IF
AreaCheck("bd1200")
Global("SanPilRidl","BD1200",0)
OR(2)
See("Bdpill01")
See("Bdpill04")
THEN
RESPONSE #100
SetGlobal("SanPilRidl","BD1200",1)
END

IF
Global("bd_ddd","global",1)
Global("SanSodQuest","Global",2)
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("SanSodQuest","Global",3)
END

IF
Global("SanHeph","LOCALS",0)
PartyHasItem("BDMisc63")
THEN
RESPONSE #100
SetGlobal("SanHeph","LOCALS",1)
END

IF
Global("SanHeph","LOCALS",0)
PartyHasItem("BDMisc21")
!AreaCheck("BD5300")
THEN
RESPONSE #100
SetGlobal("SanHeph","LOCALS",2)
END

IF
!Global("SanHeph","LOCALS",0)
Global("SanDBdoubt","LOCALS",1)
Global("SanSodQuest","Global",4)
THEN
RESPONSE #100
SetGlobal("SanSodQuest","Global",5)
END

IF
Global("SanSoDBook","LOCALS",0)
OR(2)
PartyHasItem("BDBook11")
Global("Chapter","Global",10)
OR(2)
AreaCheck("bd2000")
AreaCheck("bd3000")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanSoDBook","LOCALS",1)
END

IF
AreaCheck("bd2000")
Global("SanBhaal","bd2000",1)
See("Bdfists1")
CombatCounter(0)
THEN
RESPONSE #100
SetGlobal("SanBhaal","bd2000",2)
END

IF
Global("bd_ddd","global",3)
GlobalGT("SanSodQuest","Global",5)
Global("SanBDdream2","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanBDdream2","LOCALS",1)
END

IF
Global("Chapter","Global",11)
AreaCheck("bd4300")
GlobalLT("SanSodQuest","Global",7)
THEN
RESPONSE #100
SetGlobal("SanSodQuest","Global",7)
END

IF
Global("bd_plot","global",621)
AreaCheck("bd0104")
InParty(Myself)
Global("SanSodQuest","Global",8)
THEN
RESPONSE #100
SetGlobal("SanSodQuest","Global",9)
END

IF
Global("SanSodEntrap","Global",0)
PartyHasItem("BDBook01")
THEN
RESPONSE #100
SetGlobal("SanSodEntrap","Global",1)
END

IF   
Global("Chapter","Global",18)
Global("SanSoDSkieB","Global",0)
GlobalLT("SanSoDSkie","Global",9)
GlobalGT("SanSoDSkie","Global",3)
OR(2)
AreaCheck("ar2600")
AreaType(CITY)
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanSoDSkieB","Global",1)
END

IF
OR(15)
Global("SanPilRidl","BD1200",1)
Global("SanSodPamp","BD0130",1)
Global("Sansusp","bd0102",1)
Global("SanSodQuest","Global",1)
Global("SanSodQuest","Global",3)
Global("SanSodQuest","Global",5)
Global("SanSodQuest","Global",7)
Global("SanSodQuest","Global",9)
Global("SanSoDBook","LOCALS",1)
Global("SanBhaal","bd2000",2)
Global("SanBDdream2","LOCALS",1)
Global("SanBdInfiltrate","Global",1)
Global("bd_spawn_cultist","bd7220",2)
Global("bd_sandrah_ot","bd4100",1)
Global("SanSoDSkieB","Global",1)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject(Player1)
StartDialogNoSet("CVSandr")
END

IF
Global("SanSoDSkie","Global",2)
PartyHasItem("CVSkiDa")
OR(15)
AreaCheck("AR0203")
AreaCheck("ar0319")
AreaCheck("ar0408")
AreaCheck("ar0512")
AreaCheck("ar0703")
AreaCheck("ar0901")
AreaCheck("ar0902")
AreaCheck("ar0903")
AreaCheck("ar1010")
AreaCheck("ar1604")
AreaCheck("ar2008")
AreaCheck("ar3637")
AreaCheck("cvelm2")
AreaCheck("ra3910")
AreaCheck("rr3116")
THEN
RESPONSE #100
SetGlobal("SanSoDSkie","Global",3)
END

IF
OR(3)
Global("SanSoDSkie","Global",1)
Global("SanSoDSkie","Global",3)
Global("SanSodEntrap","Global",1)
CombatCounter(0)
InParty(Myself)
!See([ENEMY])
See(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject(Player1)
Interact("CVSandr")
END