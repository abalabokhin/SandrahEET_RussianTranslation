IF
GlobalLT("SanHealSkie","GLOBAL",3)
InParty(Myself)
InParty("CVSandr")
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealSkie","GLOBAL",1)
END

IF
Global("SanHealSkie","GLOBAL",6)
RealGlobalTimerExpired("SanSkieT","LOCALS")
AreaType(Forest)
InParty("CVSandr")
!See([ENEMY])
Delay(18)
THEN
RESPONSE #100
ClearAllActions()
StartCutSceneMode()
SetGlobal("SanHealSkie","GLOBAL",7)
CreateCreatureObjectOffset("sahobwiz",Myself,[100.100])
CreateCreatureObjectOffset("hobwiz01",Myself,[-100.-100])
CreateCreatureObjectOffset("hobwiz02",Myself,[100.-100])
CreateCreatureObjectOffset("saskwiz",Myself,[-100.100])
CreateCreatureObjectOffset("hobelite",Myself,[0.0])
EndCutSceneMode()
END

IF
Global("SanHealSkie","GLOBAL",7)
Dead("saskwiz")
InParty("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
SetGlobal("SanHealSkie","GLOBAL",8)
END

IF
Global("SanHealSkie","GLOBAL",9)
RealGlobalTimerExpired("SanSkieT","LOCALS")
InParty("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
!AreaType(DUNGEON)
!See([ENEMY])
Delay(18)
THEN
RESPONSE #100
IncrementGlobal("SanHealSkie","GLOBAL",1)
END

IF
Global("SanHealSkie","GLOBAL",11)
RealGlobalTimerExpired("SanSkieT","LOCALS")
InParty("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
GlobalGT("SkieRomanceTalk","GLOBAL",15)
!AreaType(DUNGEON)
!See([ENEMY])
Delay(18)
THEN
RESPONSE #100
IncrementGlobal("SanHealSkie","GLOBAL",1)
END

IF
Global("SkieRomanceQuest","GLOBAL",3)
InParty("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
GlobalGT("SanHealSkie","GLOBAL",11)
Global("DesignItem","LOCALS",0)
!AreaType(OUTDOOR)
OR(3)
!PartyHasItem("MISC36")
!PartyHasItem("MISC42")
!PartyHasItem("MISC45")
THEN
RESPONSE #100
SetGlobal("DesignItem","LOCALS",1)
END

IF
Global("SkieRomanceQuest","GLOBAL",3)
InParty("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
GlobalGT("SanHealSkie","GLOBAL",11)
Global("DesignItem","LOCALS",0)
Delay(18)
THEN
RESPONSE #100
SetGlobal("DesignItem","LOCALS",3)
END

IF
OR(7)
Global("SanHealSkie","GLOBAL",3)
Global("SanHealSkie","GLOBAL",5)
Global("SanHealSkie","GLOBAL",8)
Global("SanHealSkie","GLOBAL",10)
Global("SanHealSkie","GLOBAL",12)
Global("DesignItem","LOCALS",1)
Global("DesignItem","LOCALS",3)
InParty(Myself)
InParty("CVSandr")
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END