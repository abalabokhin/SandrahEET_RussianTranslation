IF
InParty(Myself)
InParty("CVSandr")
GlobalLT("SanHealKiyo","GLOBAL",4)
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealKiyo","GLOBAL",1)
END

IF
Global("SanKiyoTalks","GLOBAL",0)
InParty(Myself)
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanKiyoTalks","GLOBAL",1)
RealSetGlobalTimer("SanKiyoti","LOCALS",1400)
END

IF
RealGlobalTimerExpired("SanKiyoti","LOCALS")
OR(2)
Global("SanKiyoTalks","GLOBAL",1)
Global("SanKiyoTalks","GLOBAL",3)
InParty("CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanKiyoTalks","GLOBAL",1)
END

IF
Global("SanHealKiyo","GLOBAL",5)
OR(2)
Global("SanKiyoTalks","GLOBAL",5)
Global("SanKiyoTalks","GLOBAL",9)
RealGlobalTimerExpired("SanKiyoti","LOCALS")
InParty("Kova")
InParty("CVSandr")
!TimeOfDay(DAY)
!AreaType(DUNGEON)
!AreaCheck("ar1200")
THEN
RESPONSE #100
IncrementGlobal("SanKiyoTalks","GLOBAL",1)
END

IF
GlobalGT("BylTalk","GLOBAL",1)
Global("SanKiyoTalks","GLOBAL",7)
InParty("Kova")
InParty("CVSandr")
!StateCheck("Kova",CD_STATE_NOTVALID)
OR(15)
AreaCheck("AR0203")
AreaCheck("ar0319")
AreaCheck("ar0408")
AreaCheck("ar0512")
AreaCheck("ar0703")
AreaCheck("ar0901")
AreaCheck("ar0902")
AreaCheck("ar0903")
AreaCheck("ar1010")
AreaCheck("ar1604")
AreaCheck("ar2008")
AreaCheck("ar3637")
AreaCheck("cvelm2")
AreaCheck("ra3910")
AreaCheck("rr3116")
THEN
RESPONSE #100
IncrementGlobal("SanKiyoTalks","GLOBAL",1)
END

IF
GlobalGT("SanKiyoTalks","GLOBAL",2)
Global("Riftg03-29","GLOBAL",1)
InParty("CVSandr")
OR(2)
AreaCheck("ar0203")
AreaCheck("ar0204")
THEN
RESPONSE #100
SetGlobal("Riftg03-29","GLOBAL",2)
END

IF
GlobalGT("BKKChat","GLOBAL",2)
Global("SanKiyoTalks","GLOBAL",11)
InParty("Kova")
InParty("CVSandr")
!StateCheck("Kova",CD_STATE_NOTVALID)
AreaType(OUTDOOR)
THEN
RESPONSE #100
IncrementGlobal("SanKiyoTalks","GLOBAL",1)
END


IF
InParty(Myself)
InParty("CVSandr")
OR(8)
Global("SanHealKiyo","GLOBAL",4)
Global("SanKiyoTalks","GLOBAL",2)
Global("SanKiyoTalks","GLOBAL",4)
Global("SanKiyoTalks","GLOBAL",6)
Global("SanKiyoTalks","GLOBAL",8)
Global("SanKiyoTalks","GLOBAL",10)
Global("SanKiyoTalks","GLOBAL",12)
Global("Riftg03-29","GLOBAL",2)
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
Global("Renaljob","GLOBAL",4)
Global("ShadowHunt","GLOBAL",1)
THEN
RESPONSE #100
IncrementGlobal("ShadowHunt","GLOBAL",1)
END