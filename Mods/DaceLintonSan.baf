IF
GlobalLT("SanDaceB","GLOBAL",3)
InParty(Myself)
InParty("CVSandr")
OR(7)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
SpellCastOnMeRES("cvwoun","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanDaceB","GLOBAL",1)
END

IF
Global("SanDaceB","GLOBAL",3)
InParty("CVSandr")
OR(17)
AreaCheck("ar0314")
AreaCheck("ar0406")
AreaCheck("ar0509")
AreaCheck("ar0511")
AreaCheck("ar0513")
AreaCheck("ar0704")
AreaCheck("ar0709")
AreaCheck("ar1105")
AreaCheck("ar1602")
AreaCheck("ar2010")
AreaCheck("ar2203")
AreaCheck("ar4267")
AreaCheck("dd3302")
AreaCheck("ra5104")
AreaCheck("BG3200")
AreaCheck("ys0396")
AreaCheck("rr3305")
InParty(Myself)
THEN
RESPONSE #100
SetGlobal("SanDaceB","GLOBAL",4)
END

IF
OR(3)
Global("SanDaceB","GLOBAL",5)
Global("SanDaceB","GLOBAL",7)
Global("SanDaceB","GLOBAL",9)
RealGlobalTimerExpired("SanDaceTime","LOCALS")
InParty(Myself)
InParty("CVSandr")
!See([ENEMY])
CombatCounter(0)
AreaType(OUTDOOR)
THEN
RESPONSE #100
IncrementGlobal("SanDaceB","GLOBAL",1)
END

IF
GlobalGT("T#DaceIntTalks","GLOBAL",1)
Global("SanDaceB","GLOBAL",11)
RealGlobalTimerExpired("SanDaceTime","LOCALS")
InParty(Myself)
InParty("CVSandr")
See(Player1)
See("CVSandr")
AreaType(CITY)
TimeOfDay(DAY)
AreaType(OUTDOOR)
CombatCounter(0)
THEN
RESPONSE #100
IncrementGlobal("SanDaceB","GLOBAL",1)
RealSetGlobalTimer("SanDaceTime","LOCALS",640)
ActionOverride("CVSandr",Interact("CVSandr"))
END

IF
Global("T#DACEROMANCEACTIVE","GLOBAL",2)
RealGlobalTimerExpired("SanDaceTime","LOCALS")
Global("SanDaceB","GLOBAL",13)
InParty(Myself)
InParty("CVSandr")
TimeOfDay(DAY)
AreaType(OUTDOOR)
CombatCounter(0)
THEN
RESPONSE #100
IncrementGlobal("SanDaceB","GLOBAL",1)
END

IF
OR(5)
Global("SanDaceB","GLOBAL",4)
Global("SanDaceB","GLOBAL",6)
Global("SanDaceB","GLOBAL",8)
Global("SanDaceB","GLOBAL",10)
Global("SanDaceB","GLOBAL",14)
InParty(Myself)
InParty("CVSandr")
!See([ENEMY])
CombatCounter(0)
!StateCheck("CVSandr",CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END

IF
Global("SanDaceB","GLOBAL",15)
THEN
RESPONSE #100
SetGlobal("SanDaceB","GLOBAL",16)
ActionOverride("CVSandr",AddSpecialAbility("CVKnok"))
ActionOverride("CVSandr",AddSpecialAbility("CVTrap"))
END
