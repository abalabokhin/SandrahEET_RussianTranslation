IF
OR(2)
Global("SanJon25","GLOBAL",0)
Global("SanJon25","GLOBAL",4)
!AreaCheck("Ar4500")
InParty("CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanJon25","GLOBAL",1)
RealSetGlobalTimer("SanJonT","LOCALS",600)
END

IF
Global("SanJon25","GLOBAL",7)
!AreaType(CITY)
AreaType(OUTDOOR)
InParty("CVSandr")
See("Pellig")
Delay(12)
THEN
RESPONSE #100
SetGlobal("SanJon25","GLOBAL",8)
END

IF
GlobalGT("SanJon25","GLOBAL",5)
GlobalLT("SanHealJon","LOCALS",2)
OR(6)
SpellCastOnMe("CVSandr",CLERIC_CURE_LIGHT_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_MEDIUM_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_CURE_CRITICAL_WOUNDS)
SpellCastOnMe("CVSandr",CLERIC_HEAL)
SpellCastOnMeRES("Spin109","CVSandr")
SpellCastOnMeRES("SanHand","CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanHealJon","LOCALS",1)
END

IF
OR(2)
Global("SanJon25","GLOBAL",1)
Global("SanJon25","GLOBAL",5)
RealGlobalTimerExpired("SanJonT","LOCALS")
InParty("CVSandr")
THEN
RESPONSE #100
IncrementGlobal("SanJon25","GLOBAL",1)
END

IF
Global("SanHealJon","LOCALS",3)
Global("SanJonPwr","LOCALS",0)
GlobalGT("SanJon25","GLOBAL",8)
!InParty("Sarevok")
GlobalLT("SanSarev","GLOBAL",11)
RealGlobalTimerExpired("SanJonT","LOCALS")
InParty("CVSandr")
THEN
RESPONSE #100
SetGlobal("SanJonPwr","LOCALS",1)
END

IF
Global("CVAR3000","GLOBAL",1)
Global("LRJonCastDivinationSpell","GLOBAL",2)
InParty("CVSandr")
Global("IrSanWK","LOCALS",0)
RealGlobalTimerExpired("SanJonT","LOCALS")
Delay(24)
THEN
RESPONSE #100
SetGlobal("IrSanWK","LOCALS",1)
END

IF
GlobalGT("SanJon25","GLOBAL",8)
RealGlobalTimerExpired("SanJonT","LOCALS")
InParty("CVSandr")
HasItemEquiped("CVmysclk","CVSandr")
Global("SanIrClk","LOCALS",0)
!AreaType(OUTDOOR)
!AreaCheck("ar4500")
THEN
RESPONSE #100
SetGlobal("SanIrClk","LOCALS",1)
END

IF
Global("DevaApproach","GLOBAL",4)
InParty("CVSandr")
PartyHasItem("DBody")
Global("SanIrDeava","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanIrDeava","LOCALS",1)
END

IF
Global("LRElysium","GLOBAL",3)
Global("LRSparkTo","GLOBAL",5)
Global("SanIrDeava","LOCALS",2)
InParty("CVSandr")
AreaType(OUTDOOR)
THEN
RESPONSE #100
SetGlobal("SanIrDeava","LOCALS",3)
END

IF
Global("SanJon25","GLOBAL",9)
Global("TalkedToSolar","GLOBAL",5)
RealGlobalTimerExpired("SanJonT","LOCALS")
InParty("CVSandr")
!Global("JonUndecided","GLOBAL",3)
OR(3)
G("lrBaltSoul",1)
G("lrSendaiSoul",1)
G("lrAbazigalSoul",1)
!AreaType(OUTDOOR)
!AreaType(DUNGEON)
THEN
RESPONSE #100
SetGlobal("SanJon25","GLOBAL",10)
END

IF
Global("SealOpen","AR3019",3)
InParty("CVSandr")
CombatCounter(0)
Global("SanAtSeal","LOCALS",0)
THEN
RESPONSE #100
SetGlobal("SanAtSeal","LOCALS",1)
END

IF
InParty("CVSandr")
OR(13)
Global("SanJon25","GLOBAL",2)
Global("SanJon25","GLOBAL",3)
Global("SanJon25","GLOBAL",6)
Global("SanJon25","GLOBAL",8)
Global("SanJon25","GLOBAL",10)
Global("SanHealJon","LOCALS",2)
Global("SanJonPwr","LOCALS",1)
Global("IrSanWK","LOCALS",1)
Global("SanIrClk","LOCALS",1)
Global("SanIrDeava","LOCALS",1)
Global("SanIrDeava","LOCALS",3)
Global("SanAtSeal","LOCALS",1)
Global("SendaiDoorXP","AR6100",3)
!See([ENEMY])
See("CVSandr")
!StateCheck("CVSandr",CD_STATE_NOTVALID)
CombatCounter(0)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
MoveToObject("CVSandr")
Interact(Player1)
END